% INSTRUCTION POINTER %

subdesign ip_block
(
    addrIn[7..0] : input;
    addrOut[7..0] : output;
    mode : input; % low - increment, high - load %
    ena : input;
    clk : input;
)

variable
    reg[7..0] : dffe;
    carr[6..0] : node;

begin
    reg[].(clrn, prn, clk) = (VCC, VCC, clk);

    carr[0] =    reg[0].q;
    carr[6..1] = reg[6..1].q & carr[5..0];

    reg[].ena = ena;
    reg[0].d =    mode & addrIn[0]    # !mode & !reg[0].q;
    reg[7..1].d = mode & addrIn[7..1] # !mode & (reg[7..1].q $ carr[6..0]);

    addrOut[] = reg[].q;
end;
